plugins {
    id 'java-library'
}

group = 'com.github.drednote'
version = ''
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    // можно запустить для проверки какие пакеты подтянулись в classpath
    tasks.register("printAllDependencies", DependencyReportTask) {}

    ext {
        defaultEncoding = 'UTF-8'

        nexusRepo = System.getProperty("NEXUS_REPO") ?: NEXUS_REPO
        nexusRepoSnap = System.getProperty("NEXUS_REPO_SNAPSHOTS") ?: NEXUS_REPO_SNAPSHOTS
        nexusUser = System.getProperty("NEXUS_USER") ?: NEXUS_USER
        nexusPassword = System.getProperty("NEXUS_PASSWORD") ?: NEXUS_PASSWORD
    }

    repositories {
        mavenCentral()
        maven {
            name "nexus"
            url nexusRepo
            allowInsecureProtocol true
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        maven {
            name "nexus-snap"
            url nexusRepoSnap
            allowInsecureProtocol true
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
    }

    test {
        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
        }
        useJUnitPlatform()
        include("**/*Test.class")
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        repositories {
            maven {
                url System.getProperty("NEXUS_UPLOAD_REPO") ?: NEXUS_UPLOAD_REPO
                allowInsecureProtocol true
                credentials {
                    username System.getProperty("NEXUS_USER") ?: NEXUS_USER
                    password System.getProperty("NEXUS_PASSWORD") ?: NEXUS_PASSWORD
                }
            }
        }
    }
}
